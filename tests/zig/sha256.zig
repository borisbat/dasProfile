const std = @import("std");
const profile = @import("profile.zig");

const k = [64]u32{
    0x428a2f98, 0x71374491, 0xb5c0fbcf, 0xe9b5dba5, 0x3956c25b, 0x59f111f1, 0x923f82a4, 0xab1c5ed5,
    0xd807aa98, 0x12835b01, 0x243185be, 0x550c7dc3, 0x72be5d74, 0x80deb1fe, 0x9bdc06a7, 0xc19bf174,
    0xe49b69c1, 0xefbe4786, 0x0fc19dc6, 0x240ca1cc, 0x2de92c6f, 0x4a7484aa, 0x5cb0a9dc, 0x76f988da,
    0x983e5152, 0xa831c66d, 0xb00327c8, 0xbf597fc7, 0xc6e00bf3, 0xd5a79147, 0x06ca6351, 0x14292967,
    0x27b70a85, 0x2e1b2138, 0x4d2c6dfc, 0x53380d13, 0x650a7354, 0x766a0abb, 0x81c2c92e, 0x92722c85,
    0xa2bfe8a1, 0xa81a664b, 0xc24b8b70, 0xc76c51a3, 0xd192e819, 0xd6990624, 0xf40e3585, 0x106aa070,
    0x19a4c116, 0x1e376c08, 0x2748774c, 0x34b0bcb5, 0x391c0cb3, 0x4ed8aa4a, 0x5b9cca4f, 0x682e6ff3,
    0x748f82ee, 0x78a5636f, 0x84c87814, 0x8cc70208, 0x90befffa, 0xa4506ceb, 0xbef9a3f7, 0xc67178f2,
};

inline fn rotr(x: u32, n: u5) u32 {
    return (x >> n) | (x << @intCast(32 - @as(u6, n)));
}

noinline fn digestBlock(data: *const [16]u32, hash: *[8]u32) void {
    var digest: [64]u32 = undefined;
    for (0..16) |j| digest[j] = data[j];
    for (16..64) |j| {
        const v0 = digest[j - 15];
        const s0 = rotr(v0, 7) ^ rotr(v0, 18) ^ (v0 >> 3);
        const v1 = digest[j - 2];
        const s1 = rotr(v1, 17) ^ rotr(v1, 19) ^ (v1 >> 10);
        digest[j] = digest[j - 16] +% s0 +% digest[j - 7] +% s1;
    }
    var a = hash[0];
    var b = hash[1];
    var c = hash[2];
    var d = hash[3];
    var e = hash[4];
    var f = hash[5];
    var g = hash[6];
    var h = hash[7];
    for (0..64) |i| {
        const s0 = rotr(a, 2) ^ rotr(a, 13) ^ rotr(a, 22);
        const maj = (a & b) ^ (a & c) ^ (b & c);
        const t2 = s0 +% maj;
        const s1 = rotr(e, 6) ^ rotr(e, 11) ^ rotr(e, 25);
        const ch = (e & f) ^ ((~e) & g);
        const t1 = h +% s1 +% ch +% k[i] +% digest[i];
        h = g;
        g = f;
        f = e;
        e = d +% t1;
        d = c;
        c = b;
        b = a;
        a = t1 +% t2;
    }
    hash[0] +%= a;
    hash[1] +%= b;
    hash[2] +%= c;
    hash[3] +%= d;
    hash[4] +%= e;
    hash[5] +%= f;
    hash[6] +%= g;
    hash[7] +%= h;
}

fn sha256(msg: []const u8, out: *[64]u8) void {
    var hash = [8]u32{ 0x6a09e667, 0xbb67ae85, 0x3c6ef372, 0xa54ff53a, 0x510e527f, 0x9b05688c, 0x1f83d9ab, 0x5be0cd19 };
    const blocks = msg.len / 64;
    for (0..blocks) |i| {
        const words: *const [16]u32 = @ptrCast(@alignCast(msg[i * 64 ..][0..64]));
        digestBlock(words, &hash);
    }
    for (hash, 0..) |w, i| {
        _ = std.fmt.bufPrint(out[i * 8 ..][0..8], "{x:0>8}", .{w}) catch unreachable;
    }
}

var input: [1024]u8 align(4) = undefined;
var hex: [64]u8 = undefined;

fn run(_: void) void {
    for (0..32) |_| sha256(&input, &hex);
}

pub fn main(init: std.process.Init) !void {
    @memset(&input, '.');
    try profile.profile(init, "sha256", run, {});
    sha256(&input, &hex);
    if (!std.mem.eql(u8, &hex, "8adcaee60bb05a9964a1df12d2f007adcb8f3fa20ff7d1ecfde0a2ac301ff412")) profile.fail("sha256: wrong digest");
}
